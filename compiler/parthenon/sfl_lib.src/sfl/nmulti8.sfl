/******************************************
* (C)Copyright by N.T.T 1993(unpublished) *
* All rights are reserved.                *
******************************************/
%i "../int_h/csa16.h"
%i "../int_h/cla16.h"
module nmulti8 {
    input   in1<8> ;
    input   in2<8> ;
    output  out<16> ;
    csa16   csa0, csa1, csa2, csa3, csa4, csa5, csa6, csa7 ;
    cla16   cla ;
    sel_v   tmp0<16>, tmp1<16>, tmp2<16>, tmp3<16>, tmp4<16> ;
    sel_v   tmp5<16>, tmp6<16>, tmp7<16>, tmp8<16>, tmp9<16> ;
    sel_v   t0<16>, t1<16>, t2<16>, t3<16>, t4<16>, t5<16>, t6<16> ;
    sel_v   u0<16>, u1<16>, u2<16>, u3<16>, u4<16> ;
    sel_v   v0<16>, v1<16>, v2<16>, v3<16> ;
    sel_v   w0<16>, w1<16>, w2<16> ;
    instrin do ;
    instruct do par {
        tmp0 = 0b000000000||(  in1<6:0> & 7 # in2<0> )     ;
        tmp1 = 0b00000000||(  in1<6:0> & 7 # in2<1> )||0b0 ;
        tmp2 = 0b0000000||(  in1<6:0> & 7 # in2<2> )||0b00 ;
        tmp3 = 0b000000||(  in1<6:0> & 7 # in2<3> )||0b000 ;
        tmp4 = 0b00000||(  in1<6:0> & 7 # in2<4> )||0b0000 ;
        tmp5 = 0b0000||(  in1<6:0> & 7 # in2<5> )||0b00000 ;
        tmp6 = 0b000||(  in1<6:0> & 7 # in2<6> )||0b000000 ;
        tmp7 = 0b00||(^(in1<6:0> & 7 # in2<7>))||0b0000000 ;
        tmp8 = 0b00||(^(in2<6:0> & 7 # in1<7>))||0b0000000 ;
        tmp9 = 0b1||(  in1<7> & in2<7> )||0b00000100000000 ;
        /*------ 1 -------------------------------*/
        t0 = csa0.do(tmp0,tmp1,tmp2).out1 ;
        t1 = csa0.out2 ;
        t2 = csa1.do(tmp3,tmp4,tmp5).out1 ;
        t3 = csa1.out2 ;
        t4 = csa2.do(tmp6,tmp7,tmp8).out1 ;
        t5 = csa2.out2 ;
        /*--*/
        t6 = tmp9 ;
        /*------ 2 -------------------------------*/
        u0 = csa3.do(t0,t1,t2).out1 ;
        u1 = csa3.out2 ;
        u2 = csa4.do(t3,t4,t5).out1 ;
        u3 = csa4.out2 ;
        /*--*/
        u4 = t6 ;
        /*------ 3 -------------------------------*/
        v0 = csa5.do(u0,u1,u2).out1 ;
        v1 = csa5.out2 ;
        /*--*/
        v2 = u3 ;
        v3 = u4 ;
        /*------ 4 -------------------------------*/
        w0 = csa6.do(v0,v1,v2).out1 ;
        w1 = csa6.out2 ;
        /*--*/
        w2 = v3 ;
        /*------ 5 -------------------------------*/
        csa7.do(w0,w1,w2) ;
        /*------ cla -----------------------------*/
        out = cla.do(0b0,csa7.out1,csa7.out2).out ;
    }
}
