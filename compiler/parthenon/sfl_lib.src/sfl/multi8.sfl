/******************************************
* (C)Copyright by N.T.T 1993(unpublished) *
* All rights are reserved.                *
******************************************/
%i "../int_h/csa16.h"
%i "../int_h/cla16.h"
module multi8 {
    input   in1<8> ;
    input   in2<8> ;
    output  out<16> ;
    csa16   csa0, csa1, csa2, csa3, csa4, csa5 ;
    cla16   cla ;
    sel_v   tmp0<16>, tmp1<16>, tmp2<16>, tmp3<16> ;
    sel_v   tmp4<16>, tmp5<16>, tmp6<16>, tmp7<16> ;
    sel_v   t0<16>, t1<16>, t2<16>, t3<16>, t4<16>, t5<16> ;
    sel_v   u0<16>, u1<16>, u2<16>, u3<16> ;
    sel_v   v0<16>, v1<16>, v2<16> ;
    instrin do ;
    instruct do par {
        /*------ enb -----------------------------*/
        tmp0 = 0b00000000||( in1 & 8 # in2<0> )     ;
        tmp1 = 0b0000000||( in1 & 8 # in2<1> )||0b0 ;
        tmp2 = 0b000000||( in1 & 8 # in2<2> )||0b00 ;
        tmp3 = 0b00000||( in1 & 8 # in2<3> )||0b000 ;
        tmp4 = 0b0000||( in1 & 8 # in2<4> )||0b0000 ;
        tmp5 = 0b000||( in1 & 8 # in2<5> )||0b00000 ;
        tmp6 = 0b00||( in1 & 8 # in2<6> )||0b000000 ;
        tmp7 = 0b0||( in1 & 8 # in2<7> )||0b0000000 ;
        /*------ 1 -------------------------------*/
        t0 = csa0.do(tmp0,tmp1,tmp2).out1 ;
        t1 = csa0.out2 ;
        t2 = csa1.do(tmp3,tmp4,tmp5).out1 ;
        t3 = csa1.out2 ;
        /*--*/
        t4 = tmp6 ;
        t5 = tmp7 ;
        /*------ 2 -------------------------------*/
        u0 = csa2.do(t0,t1,t2).out1 ;
        u1 = csa2.out2 ;
        u2 = csa3.do(t3,t4,t5).out1 ;
        u3 = csa3.out2 ;
        /*------ 3 -------------------------------*/
        v0 = csa4.do(u0,u1,u2).out1 ;
        v1 = csa4.out2 ;
        /*--*/
        v2 = u3 ;
        /*------ 4 -------------------------------*/
        csa5.do(v0,v1,v2) ;
        /*------ cla -----------------------------*/
        out = cla.do(0b0,csa5.out1,csa5.out2).out ;
    }
}
